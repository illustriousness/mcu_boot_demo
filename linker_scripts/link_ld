/* Entry point */
ENTRY(Reset_Handler)

/* Memory layout */
MEMORY
{
    FLASH (rx)  : ORIGIN = 0x08000000, LENGTH = 0x7800
    RTT  (rwx)  : ORIGIN = 0x20000000, LENGTH = 0x400
    RAM  (rwx)  : ORIGIN = 0x20000400, LENGTH = 0xBC00
}

_estack = ORIGIN(RAM) + LENGTH(RAM);

/* Sections */
SECTIONS
{
    /* ================= FLASH ================= */

    .isr_vector :
    {
        KEEP(*(.isr_vector))
    } > FLASH

    .text :
    {
        *(.text*)
        *(.rodata*)
        *(.glue_7*)
        *(.glue_7t*)
        *(.eh_frame*)
        KEEP(*(.init))
        KEEP(*(.fini))
    } > FLASH

    .ARM.extab :
    {
        *(.ARM.extab* .gnu.linkonce.armextab.*)
    } > FLASH

    .ARM.exidx :
    {
        *(.ARM.exidx* .gnu.linkonce.armexidx.*)
    } > FLASH

    .preinit_array :
    {
        PROVIDE_HIDDEN(__preinit_array_start = .);
        KEEP(*(.preinit_array*))
        PROVIDE_HIDDEN(__preinit_array_end = .);
    } > FLASH

    .init_array :
    {
        PROVIDE_HIDDEN(__init_array_start = .);
        KEEP(*(SORT(.init_array.*)))
        KEEP(*(.init_array*))
        PROVIDE_HIDDEN(__init_array_end = .);
    } > FLASH

    .fini_array :
    {
        PROVIDE_HIDDEN(__fini_array_start = .);
        KEEP(*(SORT(.fini_array.*)))
        KEEP(*(.fini_array*))
        PROVIDE_HIDDEN(__fini_array_end = .);
    } > FLASH

    .rti_fn :
    {
        . = ALIGN(4);
        KEEP(*(SORT(.rti_fn.*)))
        . = ALIGN(4);
    } > FLASH

    .fsymtab :
    {
        . = ALIGN(4);
        __fsymtab_start = .;
        KEEP(*(FSymTab))
        KEEP(*(FSymTab.*))
        __fsymtab_end = .;
        . = ALIGN(4);
    } > FLASH

    . = ALIGN(4);
    __etext = .;
    _sidata = __etext;

    /* ================= RAM ================= */

    /* RTT buffer: fixed 1 KB at RAM base */
    .rtt (NOLOAD) :
    {
        . = ALIGN(4);
        *(.jlink_rtt)
        . = ALIGN(4);
    } > RTT

    /* data section */
    .data : AT(__etext)
    {
        . = ALIGN(4);
        _sdata = .;
        *(.data*)
        . = ALIGN(4);
        _edata = .;
    } > RAM

    /* bss section */
    .bss (NOLOAD) :
    {
        . = ALIGN(4);
        __bss_start = .;
        _sbss = .;
        *(.bss*)
        *(COMMON)
        . = ALIGN(4);
        __bss_end = .;
        _ebss = .;
    } > RAM

    /* end of RAM */
    _end = .;
    __end__ = .;
    end = .;
}
